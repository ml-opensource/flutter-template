plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}
// TODO: Change the appName, it will be used as app label 
def appName = "Monstarlab Flutter Template"

// TODO: Change the applicationId for production
// On staging it will add .staging and on development it will add .development as suffix.
def applicationId = "com.monstarlab.flutter"

ext {
    androidMinSdkVersion = 21
    androidCompileAndTargetSdkVersion = 34
}


android {
    namespace = "$applicationId"
    compileSdk = androidCompileAndTargetSdkVersion
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId = "$applicationId"
        // You can update the following values to match your application needs.
        // For more information, see: https://flutter.dev/to/review-gradle-config.
        minSdk = androidMinSdkVersion
        targetSdk = androidCompileAndTargetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }

    buildTypes {
        release {
            // TODO: Add your own signing config for the release build.
            // Signing with the debug keys for now, so `flutter run --release` works.
            signingConfig = signingConfigs.debug
        }
    }
    //TODO: Change resValue application name
    flavorDimensions.add("flavor-type")

    productFlavors {
        create("development") {
            dimension = "flavor-type"
            applicationIdSuffix = ".development"
            versionNameSuffix = "-development"
            manifestPlaceholders["appName"] = "[DEV] $appName"
            signingConfig = signingConfigs.getByName("debug")
        }
        create("staging") {
            dimension = "flavor-type"
            applicationIdSuffix = ".staging"
            versionNameSuffix = "-staging"
            manifestPlaceholders["appName"] = "[STG] $appName"
            signingConfig = signingConfigs.getByName("debug")
        }
        create("production") {
            dimension = "flavor-type"
            manifestPlaceholders["appName"] = "$appName"
            // TODO: After adding your own signing config for the release build,
            // Change this to
            // signingConfig = signingConfigs.getByName("release")
            signingConfig = signingConfigs.getByName("debug")
        }
    }
}

flutter {
    source = "../.."
}
